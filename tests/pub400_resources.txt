*** Settings ***
Library           Mainframe3270
#Library           String
Library           Process
Library           OperatingSystem

*** Variables ***
# host
${host}               pub400.com
# folders
${folder}      .{/}screenshot
${new_folder}      ${folder}${/}new_folder
# Credentials
${user}    USER0112
${password}    user0112
# Texts to write
${write_text}         TEST
# Texts to read with correct case
${welcome_title}    Welcome to PUB400.COM * your public IBM i server
${welcome_title_short}      Welcome to PUB400.COM
${main_menu_title}    IBM i Main Menu
${main_menu_help_title}    IBM i Main Menu - Help
${text_after_delete_char}     EST TEST
${text_after_delete_field}     ${SPACE * 8}
${text_after_move_next_field}     ${SPACE * 4}
${text_match}     *PUB???.COM*
${text_to_count}    IBM
${valid_regex}    USER\\d{4}
@{list_strings}     Server name    Subsystem    Display name
# Non matching text
@{list_strings_right_in_the_first}    Subsystem    WRONGSTRING    WRONGSTRING
@{list_strings_right_on_the_second}    WRONGSTRING    Subsystem    WRONGSTRING
@{list_strings_right_on_the_third}     WRONGSTRING    WRONGSTRING    Display name
# Texts to read with wrong case
${welcome_title_wrong_case}    WELCOME TO PUB400.COM * YOUR PUBLIC IBM I SERVER
${welcome_title_short_wrong_case}      WELCOME TO PUB400.COM
${text_match_wrong_case}     *pub???.com*
${text_to_count_wrong_case}    ibm
@{list_strings_wrong_case}     SERVER NAME    SUBSYSTEM    DISPLAY NAME
@{list_strings_right_on_the_first}   Server name    WRONGSTRING   WRONGSTRING
@{list_strings_right_in_the_first_wrong_case}    SUBSYSTEM    WRONGSTRING    WRONGSTRING
@{list_strings_right_on_the_second_wrong_case}    WRONGSTRING    SUBSYSTEM    WRONGSTRING
@{list_strings_right_on_the_third_wrong_case}    WRONGSTRING    WRONGSTRING   SUBSYSTEM
@{list_strings_wrong_case_on_the_first}    SERVER NAME    Subsystem    Display name
@{list_strings_wrong_case_on_the_second}   Server name    SUBSYSTEM   Display name
@{list_strings_wrong_case_on_the_third}    Server name   Subsystem    DISPLAY NAME
@{list_strings_wrong_on_the_first}    WRONGSTRING    Subsystem    Display name
@{list_strings_wrong_on_the_second}   Server name    WRONGSTRING   Display name
@{list_strings_wrong_on_the_third}    Server name   Subsystem    WRONGSTRING
@{list_strings_all_wrong_case}    SERVER NAME   SUBSYSTEM    DISPLAY NAME
# Texts not existants to read with correct case
@{list_strings_not_existants}     SERVER NAME    SUBSYSTEM    DISPLAY NAME
${text_not_match}     *PUB???400.COM*
${invalid_regex}    USER\\d{5}
# Texts not existants to read with wrong case
@{list_strings_not_existants_ignore_case}     WRONGSTRING    WRONGSTRING    WRONGSTRING
${text_not_match_wrong_case}     *pub???400.com*
${string_not_existant}     WRONGSTRING
# Expected errors
${welcome_text_expected_error}    ${welcome_title} != ${welcome_title_short}
${x_axis_exceed_expected_error}    You have exceeded the x-axis limit of the mainframe screen
${y_axis_exceed_expected_error}    You have exceeded the y-axis limit of the mainframe screen

*** Keywords ***
Logon
    Page Should Contain String    ${welcome_title}
    Write Bare In Position   ${user}    5    25
    Write Bare In Position   ${password}    6    25
    Send Enter
    Check if user is allocated to another job
    Check if user have messages to read
    Wait Field Detected

Logout
    Write Bare    90
    Send Enter
    Sleep    1s

Check if user is allocated to another job
    ${status}=    Run Keyword And Return Status    Page Should Contain String    is allocated to another job
    Run Keyword If    ${status}    Send Enter

Check if user have messages to read
    ${status}=    Run Keyword And Return Status    Page Should Contain String    Display Messages
    Run Keyword If    ${status}    Send Enter

Verify String Not Found
    [Arguments]    ${keyword}    ${string}    ${ignore_case}=${False}
    ${expected_error}=    Set Variable    The string "${string}" was not found
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}    ignore_case=${ignore_case}

Verify String Not Found In List
    [Arguments]    ${keyword}    ${string_list}    ${string_position}    ${ignore_case}=${False}
    ${not_found_string}=    Set Variable If    ${ignore_case}==${False}    ${string_list[${${string_position}-1}]}    ${string_list[${${string_position}-1}].lower()}
    ${expected_error}=    Set Variable    The string "${not_found_string}" was not found
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string_list}    ignore_case=${ignore_case}

Verify List Not Found
    [Arguments]    ${keyword}    ${list}    ${ignore_case}=${False}
    ${expected_error}=    Set Variable    The strings "${list}" was not found
    Run Keyword And Expect Error    EQUALS: ${expected_error}    ${keyword}    ${list}    ignore_case=${ignore_case}

Verify Pattern Not Found
    [Arguments]    ${keyword}    ${string}    ${ignore_case}=${False}
    ${not_found_string}=    Set Variable If    ${ignore_case}==${False}    ${string}    ${string.lower()}
    ${expected_error}=    Set Variable    No matches found for "${not_found_string}" pattern
    Run Keyword If    ${ignore_case}    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}    ignore_case=${ignore_case}
    ...    ELSE    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}

Verify String Does Not Appear X Times
    [Arguments]    ${keyword}    ${string}    ${wrong_number_of_times}    ${right_number_of_times}    ${ignore_case}=${False}
    ${expected_error}=    Set Variable    The string "${string}" was not found "${wrong_number_of_times}" times, it appears "${right_number_of_times}" times
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${text_to_count}    2    ignore_case=${ignore_case}

Verify String Found
    [Arguments]    ${keyword}    ${string}    ${ignore_case}=${False}
    ${expected_error}=    Set Variable    The string "${string}" was found
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}    ignore_case=${ignore_case}

Verify List Found
    [Arguments]    ${keyword}    ${string_list}    ${string_position}    ${ignore_case}=${False}
    ${expected_error}=    Set Variable    The string "${string_list[${${string_position}-1}]}" was found
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string_list}    ignore_case=${ignore_case}

Verify Wait Until String
    [Arguments]    ${keyword}    ${string}
    ${expected_error}=    Set Variable    String "${string}" not found in 5 seconds
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}

Verify Wait Until String With Timeout
    [Arguments]    ${keyword}    ${string}    ${timeout}
    ${expected_error}=    Set Variable    String "${string}" not found in ${timeout} seconds
    Run Keyword And Expect Error    ${expected_error}    ${keyword}    ${string}    ${timeout}
